dependencies {
    compile "com.djrapitops:AbstractPluginFramework-api:$abstractPluginFrameworkVersion"
    compile project(":api")
    compileOnly project(":extensions")
    compile project(path: ":extensions", configuration: 'shadow')
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    compile "org.apache.commons:commons-text:$commonsTextVersion"
    compile "org.apache.commons:commons-compress:$commonsCompressVersion"
    compile "com.googlecode.htmlcompressor:htmlcompressor:$htmlCompressorVersion"
    compile "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
    compile "com.h2database:h2:$h2Version"
    compile "mysql:mysql-connector-java:$mysqlVersion"
    compile "com.zaxxer:HikariCP:$hikariVersion"
    compile "org.slf4j:slf4j-nop:$slf4jVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "com.maxmind.geoip2:geoip2:$geoIpVersion"
    compile "com.google.code.gson:gson:$gsonVersion"

    testCompile project(":api")
    testCompile "com.google.code.gson:gson:$gsonVersion"
}

import org.apache.tools.ant.filters.ReplaceTokens
task updateVersion(type: Copy) {
    from('src/main/resources') {
        include 'plugin.yml'
        include 'bungee.yml'
        include 'nukkit.yml'
    }
    into 'build/sources/resources/'
    filter(ReplaceTokens, tokens: [version: '' + project.ext.fullVersion])
}
processResources {
    dependsOn updateVersion
    from 'build/sources/resources'
}

shadowJar {
    dependsOn processResources
    configurations = [project.configurations.compile]

    // Exclude these files
    exclude "**/*.svg"
    exclude "**/*.psd"

    exclude "**/module-info.class"
    exclude 'META-INF/versions/' // Causes Sponge to crash

    relocate('org.apache', 'plan.org.apache') {
        exclude 'org/apache/logging/**'
    }
    relocate 'com.maxmind', 'plan.com.maxmind'
    relocate 'com.fasterxml', 'plan.com.fasterxml'
    relocate 'com.zaxxer', 'plan.com.zaxxer'
    relocate 'com.github.benmanes', 'plan.com.github.benmanes'
    relocate 'com.googlecode.htmlcompressor', 'plan.com.google.htmlcompressor'
    relocate 'com.google.protobuf', 'plan.com.google.protobuf'
    relocate 'com.google.gson', 'plan.com.google.gson'
    relocate 'com.google.errorprone', 'plan.com.google.errorprone'
    relocate 'org.h2', 'plan.org.h2'
    relocate 'org.bstats', 'plan.org.bstats'
    relocate 'org.slf4j', 'plan.org.slf4j'

    // Exclude test dependencies
    exclude "org/junit/**/*"
    exclude "org/opentest4j/**/*"
    exclude "org/checkerframework/**/*"
    exclude "org/apiguardian/**/*"
    exclude "org/mockito/**/*"
    exclude "org/selenium/**/*"
    exclude "org/jayway/**/*"
    exclude "google/protobuf/**/*"
    exclude "jargs/gnu/**/*"
}